buildscript {
    ext {
        springBootVersion = '1.1.5.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "http://repo.spring.io/release" }
}

ext {
    jacksonDatabindVersion = '2.2.1'
    jerseyServletVersion = '2.7'
    jerseyClientVersion = '2.7'
    jettyClientVersion = '9.1.2.v20140210'
    commonsLang3Version = '3.1'
    jodaTimeVersion = '2.3'
    springJavaConfigVersion = '1.0.0.m3'
    hibernateValidatorVersion = '5.1.0.Final'
    cassandraVersion = '2.1.2'
    cassandraDriverCore= '2.1.3'
    jfreeChartVersion = '1.0.13'
    commonsMathVersion = '2.2'
    codaHaleMetricsVersion = '3.0.2'
    //nettyVersion = '4.0.17.Final'
    nettyVersion = '4.0.24.Final'

    testNgVersion = '6.8'
    mockitoVersion = '1.9.5'
    hamcrestVersion = '1.3'
    cassandraUnit = '2.0.2.2'
}

sourceCompatibility = 1.7
group = 'com.github.hvd617.tsa'
version = '1.0.0-SNAPSHOT'

configurations {
    // We will use testng and matcher throughout
    all*.exclude group: 'junit'
    all*.exclude group: 'org.hamcrest', module: 'hamcrest-all'

    // We will use log4j through-out.  Spring boot loads logback by default.
    all*.exclude group: 'commons-logging'
    all*.exclude group: 'ch.qos.logback'
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
    // Logging
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.5'
    compile 'org.slf4j:jcl-over-slf4j:1.7.5'
    compile 'org.slf4j:jul-to-slf4j:1.7.5'

    // Servlet
    compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-websocket:${springBootVersion}"

    // Jersey support
    compile "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyServletVersion}"
    compile "org.glassfish.jersey.ext:jersey-spring3:${jerseyServletVersion}"
    compile "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyClientVersion}"

    // Misc
    compile("com.datastax.cassandra:cassandra-driver-core:${cassandraDriverCore}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compile "org.hibernate:hibernate-validator:${hibernateValidatorVersion}"
    compile "joda-time:joda-time:${jodaTimeVersion}"
    compile "jfree:jfreechart:${jfreeChartVersion}"
    compile "org.apache.commons:commons-math:${commonsMathVersion}"

    compile "com.codahale.metrics:metrics-graphite:${codaHaleMetricsVersion}"
    compile "io.netty:netty-all:${nettyVersion}"

    // Testing
    testCompile "org.hamcrest:hamcrest-library:${hamcrestVersion}"
    testCompile "org.testng:testng:${testNgVersion}"
    testCompile "org.cassandraunit:cassandra-unit:${cassandraUnit}"
    testCompile "org.eclipse.jetty:jetty-client:${jettyClientVersion}"
    testCompile "org.eclipse.jetty.websocket:websocket-client:${jettyClientVersion}"
    testCompile "org.apache.cassandra:cassandra-all:${cassandraVersion}"

    // Jersey
    testCompile "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    testCompile "org.glassfish.jersey.core:jersey-client:${jerseyClientVersion}"
    testCompile "org.glassfish.jersey.connectors:jersey-grizzly-connector:${jerseyClientVersion}"

}

test {
    useTestNG() {
        excludeGroups 'util'
    }
    include(["**/*Tests.class", "**/*Test.class"])
    exclude(["**/Abstract*.class", '**/*$*'])
}

bootRepackage {
    mainClass = 'com.github.hiendo.tsa.config.AppMain'
    classifier = 'exec'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}